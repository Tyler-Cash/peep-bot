plugins {
    id 'application'
    id 'jacoco'
    id 'org.springframework.boot' version '3.5.11'
    id 'org.cyclonedx.bom' version '2.4.1'
}

apply plugin: 'application'
apply plugin: 'io.spring.dependency-management'

group = 'dev.tylercash'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'dev.tylercash.event.PeepBotApplication'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    springCloudVersion = '2025.0.1'
    // Overriding Spring dependency versions for security and consistency
    set('spring-framework.version', '6.2.11')
    set('spring-batch.version', '5.2.2')
    set('spring-data-bom.version', '2025.0.3')
    set('spring-security.version', '6.5.5')
    set('spring-session.version', '3.5.2')
    set('spring-retry.version', '2.0.10')
    set('thymeleaf-extras-springsecurity.version', '3.1.3.RELEASE')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}


dependencies {
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'com.ibm.icu:icu4j:78.2'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'org.hibernate.orm:hibernate-community-dialects:7.2.5.Final'
    implementation 'org.liquibase:liquibase-core'
    implementation 'org.postgresql:postgresql'
    implementation 'org.projectlombok:lombok'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.15'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j'
    implementation 'org.springframework.security:spring-security-oauth2-client'
    implementation 'org.springframework.session:spring-session-jdbc'
    implementation 'net.javacrumbs.shedlock:shedlock-spring:6.10.0'
    implementation 'net.javacrumbs.shedlock:shedlock-provider-jdbc-template:6.10.0'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5:3.1.3.RELEASE'
    implementation platform('io.micrometer:micrometer-tracing-bom:latest.release')
    implementation("net.dv8tion:JDA:5.6.1") {
        exclude module: 'opus-java'
    }

    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'

    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testImplementation "org.testcontainers:testcontainers"
    testImplementation 'com.h2database:h2'
    testImplementation 'org.springframework.batch:spring-batch-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform {
        excludeTags 'e2e'
    }
    finalizedBy jacocoTestReport
}

tasks.register('e2eTest', Test) {
    description = 'Runs e2e tests against real external services (Discord, DB). Requires credentials in environment.'
    group = 'verification'
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    useJUnitPlatform {
        includeTags 'e2e'
    }
    shouldRunAfter test
}

jacoco {
    toolVersion = '0.8.14'
}

jacocoTestReport {
    dependsOn test
    mustRunAfter e2eTest
    // Pick up exec files from both test and e2eTest when both have run
    executionData fileTree(layout.buildDirectory.dir("jacoco")).include("*.exec")
    reports {
        xml.required = true
        html.required = true
    }
}

tasks.named("bootJar") {
    launchScript()

}

tasks.bootBuildImage {
    imageName = "ghcr.io/tyler-cash/peep-bot-backend:latest"
}

